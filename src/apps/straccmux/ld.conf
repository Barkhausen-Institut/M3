#include <base/Config.h>

OUTPUT_FORMAT("elf32-littleriscv")

ENTRY(_start)

MEMORY
{
  imem (rx):     ORIGIN = 0x00000000, LENGTH = 64K
  dmem (!rx):    ORIGIN = 0x00011000, LENGTH = 12K
}

SECTIONS
{
    .text :
    {
        PROVIDE(_text_start = .);
        KEEP (*(SORT_NONE(.init)))
        *(.text*)
    } > imem

    /* ensure that the coverage info is before _text_end */
    __llvm_prf_names : {} > imem

    .rodata :
    {
        *(.rodata*)
        *(.srodata*)
        *(.sdata2 .sdata2.* .gnu.linkonce.s2.*)
        KEEP (*(SORT_NONE(.fini)))
        PROVIDE(_text_end = .);
    } > imem

    .data :
    {
        PROVIDE(_data_start = .);
        *(.data)
        *(.data.*)
        *(.sdata .sdata.* .sdata2.* .gnu.linkonce.s.*)
        *(.got.plt*)
        *(.got)
    } > dmem

    .eh_frame :
    {
        KEEP (*(.eh_frame))
        *(.eh_frame.*)
        // this is required for RISC-V (at least) to ensure that it finds the end of the frames
        LONG (0);
    } > dmem
    .gnu_extab          : { *(.gnu_extab) } > dmem
    .gcc_except_table   : { *(.gcc_except_table .gcc_except_table.*) } > dmem

    .ctors :
    {
        CTORS_BEGIN = .;
        KEEP (*(SORT_BY_INIT_PRIORITY(.init_array.*) SORT_BY_INIT_PRIORITY(.ctors.*)))
        KEEP (*(.init_array EXCLUDE_FILE (*crtbegin.o *crtbegin?.o *crtend.o *crtend?.o ) .ctors))
        CTORS_END = .;
        PROVIDE(_data_end = .);
    } > dmem

    .bss :
    {
        PROVIDE(_bss_start = .);
        . = . + 1; . = ALIGN(0x1000);
        PROVIDE(baremetal_stack = .);
        *(COMMON*)
        *(.bss*)
        *(.sbss .sbss.* .gnu.linkonce.sb.*)
        PROVIDE(_bss_end = .);
    } > dmem

    /* Thread Local Storage sections  */
    .tdata :
    {
        PROVIDE_HIDDEN (_tdata_start = .);
        *(.tdata .tdata.* .gnu.linkonce.td.*)
    } > dmem
    .tbss :
    {
        *(.tbss .tbss.* .gnu.linkonce.tb.*) *(.tcommon)
        PROVIDE_HIDDEN (_tdata_end = .);
    } > dmem
}
