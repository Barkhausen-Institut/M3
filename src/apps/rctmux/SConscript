Import('env')

# define useful variables
archdir = 'arch/' + env['ARCH']
archdir_absolute = '#src/apps/rctmux/' + archdir

# t3
if env['ARCH'] == 't3':
    rctmux = env.M3Program(
        env,
        target = 'idle',
        source = [
            env['BINARYDIR'].abspath + '/Window.o',
            env.Glob('*.cc'),
            env.Glob(archdir + '/*.S'),
            env.Glob(archdir + '/*.cc'),
        ],
        NoSup = True,
        ldscript = File(archdir_absolute + '/link-elf32.ld'),
        libs = ['c', 'handler-reset', 'hal', 'handlers-sim']
    )
    env.Depends(rctmux, File(archdir_absolute + '/link-elf32.ld'))
# gem5
elif env['ARCH'] == 'gem5':
    myenv = env.Clone()
    myenv.Append(LINKFLAGS = ' -Wl,--section-start=.text=0x3FF0000')

    # flto does not work for currently unknown reasons
    # (hi part of adresses in idt are missing for some reason)
    # we therefore remove this build option for the moment
    myenv.Replace(CXXFLAGS = str(myenv['CXXFLAGS']).replace('-flto', ''))
    myenv.Replace(CFLAGS = str(myenv['CFLAGS']).replace('-flto', ''))
    myenv.Replace(LINKFLAGS = str(myenv['LINKFLAGS']).replace('-flto', ''))

    myenv.M3Program(
        myenv,
        target = 'idle',
        source = [
            env.Glob('*.cc'),
            env.Glob(archdir + '/*.S'),
            env.Glob(archdir + '/*.cc'),
        ],
        NoSup = True,
        varAddr = False
    )

# build test and dev programs
env.M3Program(env, 'rctmux-util-tmux-bench', ['test-utils/tmux-bench.cc'])
env.M3Program(env, 'rctmux-util-counter',    ['test-utils/counter.cc'])
env.M3Program(env, 'rctmux-util-occupy',     ['test-utils/occupy.cc'])
