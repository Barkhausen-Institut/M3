#include <base/Config.h>

#if defined(__x86_64__)
OUTPUT_FORMAT("elf64-x86-64")
#elif __riscv_xlen == 64
OUTPUT_FORMAT("elf64-littleriscv")
#else
OUTPUT_FORMAT("elf32-littleriscv")
#endif

ENTRY(_start)

PHDRS
{
    text    PT_LOAD;
    data    PT_LOAD;
    bss     PT_LOAD;
    tls     PT_TLS;
}

SECTIONS
{
    . = TILEMUX_CODE_START;

    .text :
    {
        PROVIDE(_text_start = .);
        KEEP (*(SORT_NONE(.init)))
        *(.text*)

#if defined(__isr__)
        . = ALIGN(0x1000);
        PROVIDE(_user_start = .);
        *(.user_text)
        PROVIDE(_user_end = .);
        . = ALIGN(0x1000);
#endif
    } : text

    /* ensure that the coverage info is before _text_end */
    __llvm_prf_names : {} : text

    .rodata :
    {
        *(.rodata*)
        *(.srodata*)
        *(.sdata2 .sdata2.* .gnu.linkonce.s2.*)
        KEEP (*(SORT_NONE(.fini)))
        PROVIDE(_text_end = .);
    } : text

    .data ALIGN(0x1000) :
    {
        PROVIDE(_data_start = .);
        *(.data)
        *(.data.*)
        *(.sdata .sdata.* .sdata2.* .gnu.linkonce.s.*)
        *(.got.plt*)
        *(.got)
    } : data

    .eh_frame :
    {
        KEEP (*(.eh_frame))
        *(.eh_frame.*)
        // this is required for RISC-V (at least) to ensure that it finds the end of the frames
        LONG (0);
    } : data
    .gnu_extab          : { *(.gnu_extab) } : data
    .gcc_except_table   : { *(.gcc_except_table .gcc_except_table.*) } : data

    .ctors :
    {
        CTORS_BEGIN = .;
        KEEP (*(SORT_BY_INIT_PRIORITY(.init_array.*) SORT_BY_INIT_PRIORITY(.ctors.*)))
        KEEP (*(.init_array EXCLUDE_FILE (*crtbegin.o *crtbegin?.o *crtend.o *crtend?.o ) .ctors))
        CTORS_END = .;
#if defined(__x86_64__)
        // make do_global_ctors_aux not call any constructor; we'll do that ourself
        QUAD (-1);
#endif
        PROVIDE(_data_end = .);
    } : data

    .bss ALIGN(0x1000) :
    {
        PROVIDE(_bss_start = .);
#if defined(__isr__) || defined(__baremetal__)
        . = . + 1; . = ALIGN(STACK_SIZE);
#   if defined(__isr__)
        . = . + 1; . = ALIGN(STACK_SIZE);
        PROVIDE(isr_stack = .);
#   endif
#   if defined(__baremetal__)
        . = . + 1; . = ALIGN(STACK_SIZE);
        PROVIDE(baremetal_stack = .);
#   endif
#endif
        *(COMMON*)
        *(.bss*)
        *(.sbss .sbss.* .gnu.linkonce.sb.*)
        PROVIDE(_bss_end = .);
    } : bss

    /* Thread Local Storage sections  */
    .tdata :
    {
        PROVIDE_HIDDEN (_tdata_start = .);
        *(.tdata .tdata.* .gnu.linkonce.td.*)
    } : tls
    .tbss :
    {
        *(.tbss .tbss.* .gnu.linkonce.tb.*) *(.tcommon)
        PROVIDE_HIDDEN (_tdata_end = .);
    } : tls
}
