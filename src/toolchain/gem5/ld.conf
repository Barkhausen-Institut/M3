OUTPUT_FORMAT("elf64-x86-64")
ENTRY(_start)

PHDRS
{
    text    PT_LOAD;
    data    PT_LOAD;
    bss     PT_LOAD;
}

SECTIONS
{
    . = 0x100000;

    .text :
    {
        PROVIDE(_text_start = .);
        *(.text*)
    } : text = 0x90909090

    .rodata :
    {
        *(.rodata*)
    } : text

    .ctors ALIGN(0x1000) :
    {
        CTORS_BEGIN = .;
        KEEP(*(SORT(.init_array.*)))
        KEEP(*(.init_array))
        CTORS_END = .;
        PROVIDE(_text_end = .);
    } : data

    .data :
    {
        PROVIDE(_data_start = .);
        *(.data)
        *(.data.*)
    } : data

    .bss ALIGN(0x1000) :
    {
        *(COMMON*)
        *(.bss*)
        PROVIDE(_bss_end = .);
    } : bss
}
