Import('env')
myenv = env.Clone()
myenv.Append(CPPPATH = ['lwip/include', 'lwip/port/include'])

# silence warnings in lwip code
lwenv = myenv.Clone()
lwenv.Append(CFLAGS = ' -Wno-sign-conversion')
lwsrc = [
    Glob('lwip/api/*.c'),
    Glob('lwip/core/*.c'),
    Glob('lwip/core/ipv4/*.c'),
    Glob('lwip/core/ipv6/*.c'),
    Glob('lwip/netif/*.c'),
    Glob('lwip/port/*.c'),
    Glob('lwip/port/*.cc'),
]
lwobj = [lwenv.Object(source = f) for f in lwsrc]

myenv.M3Program(
    myenv,
    target = 'net',
    source = [
        Glob('*.cc'),
        myenv.Glob('driver/$ARCH/*.cc'),
        Glob('sess/*.cc'),
        Glob('sess/socket/*.cc'),
    ] + lwobj,
    libs = ['pci'] if myenv['ARCH'] == 'gem5' else []
)
